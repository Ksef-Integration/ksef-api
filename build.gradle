plugins {
    id 'org.openapi.generator' version '7.6.0'
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.20.1'
}

group = 'com.ksef'

scmVersion {
    tag {
        prefix = 'v'
    }
    versionIncrementer 'incrementPatch'
}

project.version = scmVersion.version

ext {
    springBootVersion = '3.5.3'
    jacksonKotlinVersion = '2.19.1'
    swaggerAnnotationsVersion = '2.2.31'
    springdocVersion = '2.8.9'
    jacksonDatabindVersion = '0.2.6'
}

repositories {
    mavenCentral()
}

def generatedDir = layout.buildDirectory.dir("generated/api").get().asFile

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    main {
        java.srcDirs += file("${generatedDir}/src/main/java")
    }
}

dependencies {
    implementation "org.openapitools:jackson-databind-nullable:$jacksonDatabindVersion"

    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.19.1"
    implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.19.1"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.19.1"


    compileOnly "io.swagger.core.v3:swagger-annotations:$swaggerAnnotationsVersion"
    compileOnly "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"
}

tasks.withType(JavaCompile).configureEach {
    dependsOn tasks.named("openApiGenerate")
}

tasks.named("compileJava") {
    dependsOn tasks.named("openApiGenerate")
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn tasks.named("openApiGenerate")
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/openapi/ksef-openapi.yaml")
    outputDir.set(generatedDir.toString())

    apiPackage.set("com.ksef.api.client")
    invokerPackage.set("com.ksef.api.client.invoker")
    modelPackage.set("com.ksef.api.client.model")

    configOptions.set([
            "library": "resttemplate",
            "dateLibrary": "java8",
            "useRuntimeException": "true",
            "useJakartaEe": "true"
    ])
}

tasks.named("openApiGenerate") {
    doFirst {
        delete generatedDir
    }
}

tasks.register("generateServer") {
    dependsOn tasks.named("openApiGenerate")
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = "ksef-api"
            version = project.version
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Ksef-Integration/ksef-api")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token")
            }
        }
    }
}