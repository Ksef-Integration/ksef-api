plugins {
    id 'org.openapi.generator' version '7.6.0'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'java'
    id 'maven-publish'
    id 'net.researchgate.release' version '3.0.2'
}

group = 'com.calendar.api'

// Odczyt wersji z gradle.properties (release plugin wymaga tego)
def baseVersion = project.version
def gitRef = System.getenv("GITHUB_REF") ?: "refs/heads/local"
def isMasterBranch = gitRef.endsWith("/master")

// Dodanie -SNAPSHOT na gałęziach innych niż master
version = isMasterBranch ? baseVersion : "${baseVersion}-SNAPSHOT"

repositories {
    mavenCentral()
}

def generatedDir = layout.buildDirectory.dir("generated/api")

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/api/src/main/kotlin"
        }
        kotlin {
            srcDir "${buildDir}/generated/api/src/main/kotlin"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    dependsOn tasks.openApiGenerate
    kotlinOptions.jvmTarget = "21"
}

tasks.named("compileJava") {
    dependsOn tasks.openApiGenerate
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn tasks.openApiGenerate
}

openApiGenerate {
    generatorName.set("kotlin-spring")
    inputSpec.set("${rootDir}/api/main-api-file.yaml")
    outputDir.set(generatedDir.get().asFile.absolutePath)

    apiPackage.set("com.calendar.api.api")
    invokerPackage.set("com.calendar.api.invoker")
    modelPackage.set("com.calendar.api.model")

    configOptions.put("interfaceOnly", "true")
    configOptions.put("dateLibrary", "java8")
    configOptions.put("useSpringBoot3", "true")
    configOptions.put("reactive", "false")
}

tasks.register('generateServer') {
    dependsOn tasks.openApiGenerate
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'calendar-api'
            version = project.version
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/collab-timekit/calendar-api")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token")
            }
        }
    }
}

release {
    tagTemplate = 'v${version}'
    versionPropertyFile = 'gradle.properties'
    failOnSnapshotDependencies = false
    preTagCommitMessage = '[Release] - pre tag'
    tagCommitMessage = '[Release] - tag v${version}'
    newVersionCommitMessage = '[Release] - next version bumped to $version'

    git {
        requireBranch.set('master')
    }
}