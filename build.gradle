import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.openapi.generator' version '7.6.0'
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'java'
    id 'maven-publish'
    id 'net.researchgate.release' version '3.0.2'
}

group = 'com.calendar.api'

// Wersja i gałąź
def baseVersion = project.version
def gitRef = System.getenv("GITHUB_REF") ?: "refs/heads/local"
def isMasterBranch = gitRef.endsWith("/master")
version = isMasterBranch ? baseVersion : "${baseVersion}-SNAPSHOT"

// Wersje zależności
ext {
    springBootVersion = '3.2.0'
}

repositories {
    mavenCentral()
}

def generatedDir = "$buildDir/generated/api"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java.srcDir("${buildDir}/generated/api/src/main/kotlin")
    }
}

dependencies {
    compileOnly "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compileOnly "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    compileOnly 'com.fasterxml.jackson.module:jackson-module-kotlin'
    compileOnly 'org.jetbrains.kotlin:kotlin-reflect'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compileOnly 'io.swagger.core.v3:swagger-annotations:2.2.21'
    compileOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
}

tasks.withType(KotlinCompile).configureEach {
    dependsOn tasks.openApiGenerate
    kotlinOptions.jvmTarget = "21"
}

tasks.named("compileJava") {
    dependsOn tasks.openApiGenerate
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn tasks.openApiGenerate
}

openApiGenerate {
    generatorName.set("kotlin-spring")
    inputSpec.set("${rootDir}/api/main-api-file.yaml")
    outputDir.set(generatedDir)

    apiPackage.set("com.calendar.api.api")
    invokerPackage.set("com.calendar.api.invoker")
    modelPackage.set("com.calendar.api.model")

    configOptions.put("interfaceOnly", "true")
    configOptions.put("dateLibrary", "java8")
    configOptions.put("useSpringBoot3", "true")
    configOptions.put("reactive", "false")
}

// Czyszczenie katalogu przed generacją OpenAPI
tasks.named("openApiGenerate") {
    doFirst {
        delete file(generatedDir)
    }
}

tasks.register('generateServer') {
    dependsOn tasks.openApiGenerate
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'calendar-api'
            version = project.version
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/collab-timekit/calendar-api")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token")
            }
        }
    }
}

release {
    tagTemplate = 'v${version}'
    versionPropertyFile = 'gradle.properties'
    failOnSnapshotDependencies = false
    preTagCommitMessage = '[Release] - pre tag'
    tagCommitMessage = '[Release] - tag v${version}'
    newVersionCommitMessage = '[Release] - next version bumped to $version'

    git {
        requireBranch.set('master')
    }
}